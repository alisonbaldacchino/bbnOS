# E820 - Memory Detection

__Note:__ Detecting memory using `E820` is used in BIOS and it is a robust way for memory detection. For UEFI systems,  using `GetMemoryMethod()` can be used by the OS to query information about memory resources.

bbnOS uses the `E820` method for memory detection. This way can make the paging more space-efficient!

## A. General Idea

The BIOS provides a function to obtain the device's memory table: `INT 0x15, EAX=0xE820`, which is commonly named `E820`. The linux kernel also has some codes using `E820` for memory detection, but I don't know whether they still exist. As _osdev_ puts:
> It is the only BIOS function that can detect memory above 4G. It is meant to be the ultimate memory detection BIOS function

The function returns a region of memory entries, and the entries are stored at `ES:DI`, the structure is as follows:

```cpp
struct mem_entry {
    uint64_t base_addr;         // the base address
    uint64_t len;               // the length
    uint32_t type;              // the type, see below
    uint32_t acpi_30_reserved;  // for ACPI 3.0
}
```

So in total, the entry is 20 or 24 bytes long.

> __1__. There are 5 types of a region, as is shown below:
>
>__Type 1:__ Unused (Normal) RAM\
>__Type 2:__ Reserved - unusable\
>__Type 3:__ ACPI reclaimable memory\
>__Type 4:__ ACPI NVS memory\
>__Type 5:__ Area containing bad memory\
> 
> __2__. The ACPI 3.0 extended attributes field\
> _if 24 bytes are returned, it is included in the entry_ 

## B. The procedure

The [osdev wiki](https://wiki.osdev.org/Detecting_Memory_(x86)#Getting_an_E820_Memory_Map) provides a detailed code for how to do E820, here simply explains the prodecure.

The code below is directly copied from osdev:

```assembly
; use the INT 0x15, eax= 0xE820 BIOS function to get a memory map
; note: initially di is 0, be sure to set it to a value so that the BIOS code will not be overwritten. 
;       The consequence of overwriting the BIOS code will lead to problems like getting stuck in `int 0x15`
; inputs: es:di -> destination buffer for 24 byte entries
; outputs: bp = entry count, trashes all registers except esi
mmap_ent equ 0x8000             ; the number of entries will be stored at 0x8000
do_e820:
        mov di, 0x8004          ; Set di to 0x8004. Otherwise this code will get stuck in `int 0x15` after some entries are fetched 
	xor ebx, ebx		; ebx must be 0 to start
	xor bp, bp		; keep an entry count in bp
	mov edx, 0x0534D4150	; Place "SMAP" into edx
	mov eax, 0xe820
	mov [es:di + 20], dword 1	; force a valid ACPI 3.X entry
	mov ecx, 24		; ask for 24 bytes
	int 0x15
	jc short .failed	; carry set on first call means "unsupported function"
	mov edx, 0x0534D4150	; Some BIOSes apparently trash this register?
	cmp eax, edx		; on success, eax must have been reset to "SMAP"
	jne short .failed
	test ebx, ebx		; ebx = 0 implies list is only 1 entry long (worthless)
	je short .failed
	jmp short .jmpin
.e820lp:
	mov eax, 0xe820		; eax, ecx get trashed on every int 0x15 call
	mov [es:di + 20], dword 1	; force a valid ACPI 3.X entry
	mov ecx, 24		; ask for 24 bytes again
	int 0x15
	jc short .e820f		; carry set means "end of list already reached"
	mov edx, 0x0534D4150	; repair potentially trashed register
.jmpin:
	jcxz .skipent		; skip any 0 length entries
	cmp cl, 20		; got a 24 byte ACPI 3.X response?
	jbe short .notext
	test byte [es:di + 20], 1	; if so: is the "ignore this data" bit clear?
	je short .skipent
.notext:
	mov ecx, [es:di + 8]	; get lower uint32_t of memory region length
	or ecx, [es:di + 12]	; "or" it with upper uint32_t to test for zero
	jz .skipent		; if length uint64_t is 0, skip entry
	inc bp			; got a good entry: ++count, move to next storage spot
	add di, 24
.skipent:
	test ebx, ebx		; if ebx resets to 0, list is complete
	jne short .e820lp
.e820f:
	mov [mmap_ent], bp	; store the entry count
	clc			; there is "jc" on end of list to this point, so the carry must be cleared
	ret
.failed:
	stc			; "function unsupported" error exit
	ret
```

## C. Memory detection in bbnOS

bbnOS also uses the E820 method for memory detection(see boot/loader.s). It is almost the copy of osdev's code, except that in data section, I set aside some space for the return of E820, which is located at `es:di`. And when entering protected mode, it uses another function called PrintMemInfo, it prints out all memory information in the check buffer. And it should be noted that in protected mode, we should use the offset, instead of the address. For example, if the buffer is in `MemChkBuf`, to get the buffer in protected mode, we should use segment:offset, while the segment starts at 0(see GDT), the offset should be `MemChkBuf + BaseOfLoaderPhyAddr`, which is `the physical address of loader + offset of the buffer in loader`